load("@npm//scss-bundle:index.bzl", "scss_bundle")
load(
    "//src/angular-core:config.bzl",
    "CORE_ENTRYPOINTS",
    "CORE_MARKDOWN_TARGETS",
    "CORE_TARGETS",
    "CORE_TESTING_TARGETS",
)
load(
    "//tools:defaults.bzl",
    "markdown_to_html",
    "ng_module",
    "ng_package",
)

package(default_visibility = ["//visibility:public"])

# Root "@sbb-esta/angular-core" entry-point.
ng_module(
    name = "angular-core",
    srcs = glob(
        ["*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    module_name = "@sbb-esta/angular-core",
    deps = ["//src/angular-core/%s" % p for p in CORE_ENTRYPOINTS],
)

scss_bundle(
    name = "styles_bundle",
    outs = ["_styles.scss"],
    args = [
        "--entryFile=$(location :_style_bundle.scss)",
        "--outFile=$(location :_styles.scss)",
    ],
    data = [
        ":_style_bundle.scss",
        # Config file is required by "scss-bundle" and will be automatically
        # loaded by the CLI. It expects the config to be in the execroot.
        "//:scss-bundle.config.json",
        "//src/angular-core/styles:core_scss_lib",
    ],
)

markdown_to_html(
    name = "overview",
    srcs = [
        ":getting-started.md",
    ],
)

filegroup(
    name = "overviews",
    srcs = [":overview"] + ["//src/angular-core/%s:overview" % name for name in CORE_MARKDOWN_TARGETS],
)

# Creates the @sbb-esta/angular-core package published to npm.
ng_package(
    name = "npm_package",
    srcs = [
        "package.json",
    ],
    data = [
        ":styles_bundle",
        "//src/angular-core/styles:legacy_scss",
    ] + glob(["i18n/**/*.xlf"]),
    entry_point = ":public-api.ts",
    entry_point_name = "angular-core",
    nested_packages = ["//src/angular-core/schematics:npm_package"],
    tags = ["release-package"],
    deps = CORE_TARGETS + CORE_TESTING_TARGETS,
)
