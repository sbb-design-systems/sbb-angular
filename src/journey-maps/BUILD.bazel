load(
    "//src/journey-maps:config.bzl",
    "JOURNEY_MAPS_ENTRYPOINTS",
    "JOURNEY_MAPS_MARKDOWN_TARGETS",
    "JOURNEY_MAPS_TARGETS",
    "JOURNEY_MAPS_TESTING_TARGETS",
)
load(
    "//tools:defaults.bzl",
    "markdown_to_html",
    "ng_package",
    "ng_project",
)
load("@npm//:defs.bzl", "npm_link_all_packages")

package(default_visibility = ["//visibility:public"])

npm_link_all_packages()

# Root "@sbb-esta/journey-maps" entry-point.
ng_project(
    name = "journey-maps",
    srcs = glob(
        ["*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    deps = ["//src/journey-maps/%s" % p for p in JOURNEY_MAPS_ENTRYPOINTS],
)

markdown_to_html(
    name = "overview",
    srcs = glob(["*.md"]),
)

filegroup(
    name = "overviews",
    srcs = [":overview"] + ["//src/journey-maps/%s:overview" % name for name in JOURNEY_MAPS_MARKDOWN_TARGETS],
)

# Creates the @sbb-esta/journey-maps package published to npm.
ng_package(
    name = "npm_package",
    package_name = "@sbb-esta/journey-maps",
    srcs = [
        "package.json",
    ],
    nested_packages = ["//src/journey-maps/web-component:npm_package"],
    tags = ["release-package"],
    deps = JOURNEY_MAPS_TARGETS + JOURNEY_MAPS_TESTING_TARGETS,
)
