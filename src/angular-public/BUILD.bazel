load("@npm//scss-bundle:index.bzl", "scss_bundle")
load(
    "//src/angular-public:config.bzl",
    "PUBLIC_ENTRYPOINTS",
    "PUBLIC_MARKDOWN_TARGETS",
    "PUBLIC_TARGETS",
    "PUBLIC_TESTING_TARGETS",
)
load(
    "//tools:defaults.bzl",
    "markdown_to_html",
    "ng_module",
    "ng_package",
    "sass_binary",
)

package(default_visibility = ["//visibility:public"])

# Root "@sbb-esta/angular-public" entry-point.
ng_module(
    name = "angular-public",
    srcs = glob(
        ["*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    module_name = "@sbb-esta/angular-public",
    deps = ["//src/angular-public/%s" % p for p in PUBLIC_ENTRYPOINTS] + [
        "//src/angular-core",
    ],
)

sass_binary(
    name = "typography",
    src = "typography.scss",
    deps = ["//src/angular-core/styles:core_scss_lib"],
)

scss_bundle(
    name = "styles_bundle",
    outs = ["_styles.scss"],
    args = [
        "--entryFile=$(location :_style_bundle.scss)",
        "--outFile=$(location :_styles.scss)",
    ],
    data = [
        ":_style_bundle.scss",
        # Config file is required by "scss-bundle" and will be automatically
        # loaded by the CLI. It expects the config to be in the execroot.
        "//:scss-bundle.config.json",
        "//src/angular-core/styles:core_scss_lib",
    ],
)

markdown_to_html(
    name = "overview",
    srcs = [
        ":README.md",
        ":getting-started.md",
        ":i18n.md",
        ":typography.md",
    ],
)

filegroup(
    name = "overviews",
    srcs = [":overview"] + ["//src/angular-public/%s:overview" % name for name in PUBLIC_MARKDOWN_TARGETS],
)

# Creates the @sbb-esta/angular-public package published to npm.
ng_package(
    name = "npm_package",
    srcs = [
        "package.json",
    ],
    data = [
        ":styles_bundle",
        ":typography",
    ],
    entry_point = ":public-api.ts",
    entry_point_name = "angular-public",
    nested_packages = ["//src/angular-public/schematics:npm_package"],
    readme_md = "README.md",
    tags = ["release-package"],
    deps = PUBLIC_TARGETS + PUBLIC_TESTING_TARGETS,
)
