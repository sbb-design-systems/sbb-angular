load("@npm//scss-bundle:index.bzl", "scss_bundle")
load(
    "//src/angular-business:config.bzl",
    "BUSINESS_ENTRYPOINTS",
    "BUSINESS_MARKDOWN_TARGETS",
    "BUSINESS_TARGETS",
    "BUSINESS_TESTING_TARGETS",
)
load(
    "//tools:defaults.bzl",
    "markdown_to_html",
    "ng_module",
    "ng_package",
    "sass_binary",
)

package(default_visibility = ["//visibility:public"])

# Root "@sbb-esta/angular-business" entry-point.
ng_module(
    name = "angular-business",
    srcs = glob(
        ["*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    deps = ["//src/angular-business/%s" % p for p in BUSINESS_ENTRYPOINTS],
)

sass_binary(
    name = "typography",
    src = "typography.scss",
    deps = ["//src/angular-core/styles:core_scss_lib"],
)

scss_bundle(
    name = "styles_bundle",
    outs = ["_styles.scss"],
    args = [
        "--entryFile=$(location :_style_bundle.scss)",
        "--outFile=$(location :_styles.scss)",
    ],
    data = [
        ":_style_bundle.scss",
        # Config file is required by "scss-bundle" and will be automatically
        # loaded by the CLI. It expects the config to be in the execroot.
        "//:scss-bundle.config.json",
        "//src/angular-core/styles:core_scss_lib",
    ],
)

markdown_to_html(
    name = "overview",
    srcs = glob(["*.md"]),
)

filegroup(
    name = "overviews",
    srcs = [":overview"] + ["//src/angular-business/%s:overview" % name for name in BUSINESS_MARKDOWN_TARGETS],
)

# Creates the @sbb-esta/angular-business package published to npm.
ng_package(
    name = "npm_package",
    srcs = [
        "package.json",
    ],
    data = [
        ":styles_bundle",
        ":typography",
    ],
    entry_point = ":public-api.ts",
    entry_point_name = "angular-business",
    nested_packages = ["//src/angular-business/schematics:npm_package"],
    tags = ["release-package"],
    deps = BUSINESS_TARGETS + BUSINESS_TESTING_TARGETS,
)
