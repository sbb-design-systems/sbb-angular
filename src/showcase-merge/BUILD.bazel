load("@build_bazel_rules_nodejs//:index.bzl", "pkg_web")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")
load("@npm//@bazel/concatjs:index.bzl", "concatjs_devserver")
load("@npm//history-server:index.bzl", "history_server")
load("@npm//html-insert-assets:index.bzl", "html_insert_assets")
load("//tools:defaults.bzl", "esbuild", "ng_ts_library")

package(default_visibility = ["//visibility:public"])

# Run the sass compiler to output "styles.css"
sass_binary(
    name = "styles",
    src = "styles.scss",
    include_paths = ["external/npm/node_modules"],
    sourcemap = False,
    deps = ["//src/angular/styles:typography_lib"],
)

ng_ts_library(
    name = "showcase-merge",
    srcs = glob(
        include = ["*.ts"],
        exclude = [
            "**/*.spec.ts",
            "main.ts",
        ],
    ),
    tsconfig = ":tsconfig.json",
    deps = [
        "//src/showcase-merge/app",
        "@npm//@angular/core",
        "@npm//@angular/platform-browser",
    ],
)

_ASSETS = [
    ":styles.css",
    "@npm//:node_modules/zone.js/dist/zone.min.js",
]

# Insert script tags into index.html
# Supports differential loading by providing a.js and a.mjs
html_insert_assets(
    name = "index_html_dev",
    outs = ["index.html"],
    args = [
        "--html=$(execpath :index.dev.html)",
        "--out=$@",
        "--roots=. $(RULEDIR)",
        "--scripts",
        "--nomodule",
        "$(execpath @npm//:node_modules/core-js/client/core.min.js)",
        "--assets",
    ] + ["$(execpath %s)" % s for s in _ASSETS] + [
        # This file doesn't exist during the build, but will be served by concatjs_devserver
        "./_/ts_scripts.js",
    ],
    data = [
        ":index.dev.html",
        "@npm//:node_modules/core-js/client/core.min.js",
    ] + _ASSETS,
)

# This devserver is written in Go and is super-fast.
# It doesn't run any bundler or code splitter. Instead, it concatenates
# named UMD and named AMD JavaScript code on-the-fly in-memory.
# This scales really well for massive codebases.
concatjs_devserver(
    name = "devserver",
    additional_root_paths = [
        "sbb_angular/src/showcase-merge/_",
        "npm/node_modules/core-js/client",
    ],
    entry_module = "sbb_angular/src/showcase-merge/main.dev",
    port = 4200,
    scripts = [
        "@npm//:node_modules/tslib/tslib.js",
        "//tools/umd-modules:ng-localize",
        "//tools/umd-modules:rxjs",
        "//tools/umd-modules:arcgis",
    ],
    static_files = _ASSETS + [
        "@npm//:node_modules/core-js/client/core.min.js",
        ":index_html_dev",
        "//src/showcase-merge/assets",
    ],
    deps = [
        "//src/showcase-merge",
        "//src/showcase-merge/assets",
    ],
)

esbuild(
    name = "bundle-esbuild",
    args = [
        "--resolve-extensions=.mjs,.js",
    ],
    entry_point = "main.prod.ts",
    format = "esm",
    minify = True,
    output_dir = True,
    tags = ["prod-showcase"],
    deps = [
        ":showcase-merge",
    ],
)

# Insert script tags into index.html
# Supports differential loading by providing a.js and a.mjs
html_insert_assets(
    name = "index_html_prod",
    # we can't output "src/showcase-merge/index.html" since that collides with the devmode file.
    # pkg_web rule will re-root paths that start with _{name} by default
    # so we output "_prodapp/src/index.html" so that it is mapped to
    # `index.html` in the web package.
    outs = ["_prodapp/src/index.html"],
    args = [
        "--html=$(execpath :index.prod.html)",
        "--out=$@",
        "--roots=. $(RULEDIR)/_prodapp/src $(RULEDIR)",
        "--assets",
    ] + ["$(execpath %s)" % s for s in _ASSETS] + [
        "ng_localize.js",
        "--scripts --module bundle-esbuild/main.prod.js",
    ],
    data = _ASSETS + [
        ":index.prod.html",
    ],
    tags = ["prod-showcase"],
)

pkg_web(
    name = "prodapp",
    srcs = _ASSETS + [
        ":bundle-esbuild",
        ":index_html_prod",
        "//src/showcase-merge/assets",
        "//tools/umd-modules:ng-localize",
    ],
    # In production mode we serve some polyfills with script tags that have hard-coded paths in the index.html
    # so we must serve them at that path, by stripping a prefix
    additional_root_paths = [
        "src/showcase-merge/_prodapp/src",
        "tools/umd-modules",
    ],
    tags = ["prod-showcase"],
)

history_server(
    name = "prodserver",
    args = [
        "--port",
        "4200",
    ],
    data = [":prodapp"],
    tags = ["prod-showcase"],
    templated_args = [
        "-a",
        "$$(rlocation $(rootpath :prodapp))",
    ],
)

exports_files([
    "tsconfig.json",
])
